<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" minWidth="400" alpha="0.75" backgroundColor="0x333333"
	borderStyle="solid" borderThickness="1" borderColor="0x525252" clipContent="false" horizontalScrollPolicy="off" verticalScrollPolicy="off" addedToStage="init()">

	<mx:Script>
		<![CDATA[
			import ink.Ink;
			import mx.graphics.Stroke;
			import ink.InkUtils;
			import ink.InkStroke;
			import mx.core.UIComponent;
			import tools.SideCarBackend;
			import mx.collections.XMLListCollection;
			import mx.controls.Tree;
			import mx.controls.Alert;
			import tools.Images;
			
			[Bindable]
			private var paperUITreeData:XMLListCollection;

			private var timerExpandTree:Timer;
			private var selectedNode:XML;
			
			// indexed by _SheetName... your sheets better have unique names!
			// when we find the sheet, we can add a region as a child of that sheet item.
			private var sheets:Dictionary = new Dictionary();
			
			// indexed by _SheetName_RegionName... your regions better have unique names within their parent sheet!
			// when we find the region, we can add a handler as a child of that region item
			private var regions:Dictionary = new Dictionary();

			private var penArray:Array = new Array();
			private var sideCar:SideCarBackend;
			private var inkContainer:UIComponent;	
			private var currInkStroke:InkStroke;
			private var xMinOffset:Number = -1;
			private var yMinOffset:Number = -1;
			private var inkWell:Ink;			
			
			private var totalNumStrokesCaptured:uint = 0;
			
			// even if we don't get a "new segment", we should dump the children every few strokes...
			// we should reset the xOffset/yOffset on a new segment
			public function penDown(id:int, x:String, y:String, newSegment:Boolean=false):void {
				setPenActive(id);

				if (newSegment || inkWell.numStrokes == 3) {
					if (inkWell != null) {
						inkContainer.removeChild(inkWell);
					}
					inkWell = new Ink();
	   				inkWell.x = inkStrokeCanvas.width/4;
	   				inkWell.y = inkStrokeCanvas.height/4;
	   				inkContainer.addChild(inkWell);
				}

				// start up a new stroke
   				currInkStroke = new InkStroke();
   				currInkStroke.inkWidth = 2;
			}
			public function penUp(id:int, x:String, y:String):InkStroke {
				setPenNotActive(id);
				currInkStroke.rerenderWithCurves();

				// add it once we have added all the samples
   				inkWell.addStroke(currInkStroke);
				
				var sX:Number = inkStrokeCanvas.width/(inkWell.maxX - inkWell.minX);
				var sY:Number = inkStrokeCanvas.height/(inkWell.maxY - inkWell.minY);
				var minScale:Number = Math.min(sX, sY);
								
				if (minScale < 1) { // do not scale up
					inkWell.scaleX = minScale;
					inkWell.scaleY = minScale;				
				}
				
				inkWell.recenterTheCenter(new Rectangle(0, 0, inkStrokeCanvas.width, inkStrokeCanvas.height));
				totalNumStrokesCaptured++;
				totalNumStrokes.text = totalNumStrokesCaptured + " total strokes";
				return currInkStroke; // we should have a clone method...
			}
			public function penSample(id:int, xStr:String, yStr:String):void {
				var xVal:Number = 0;
				xVal = InkUtils.getCoordinateValueFromString(xStr);
				// Figure out a minimum offset to reduce these large numbers!
				if (xMinOffset == -1) { // uninitialized
					xMinOffset = xVal;
				}
				xVal = xVal - xMinOffset;
	
				var yVal:Number = 0;
				yVal = InkUtils.getCoordinateValueFromString(yStr);
				// Figure out a minimum offset to reduce these large numbers!
				if (yMinOffset == -1) { // uninitialized
					yMinOffset = yVal;
				}
				yVal = yVal - yMinOffset;
	
				// add samples to the current stroke
				currInkStroke.addPoint(xVal, yVal);
			}


		
			public function init():void {
				inkContainer = new UIComponent();
				inkStrokeCanvas.clipContent = true;
				inkStrokeCanvas.addChild(inkContainer);
				
				timerExpandTree = new Timer(400);
				timerExpandTree.addEventListener(TimerEvent.TIMER, timerExpandHandler);
				paperUITreeData = new XMLListCollection();
				paperUITree.dataProvider = paperUITreeData;
				penArray.push(pen1, pen2, pen3, pen4);
			}

			public function setSideCar(sc:SideCarBackend):void {
				sideCar = sc;
			}

			public function addSheetItem(sheetName:String):void {
				// assume you won't add two sheets with the same name!
				if (sheets[sheetName] != null) {
					return;
				}
				var item:XML = <sheet label={sheetName} nodeType="Sheet"/>;
				sheets[sheetName] = item;
				paperUITreeData.addItem(item);
				timerExpandTree.start();
			}
			
			// we keep a map of sheetNames to XML sheet items... we can append children faster that way
			public function addRegionItem(regionName:String, sheetName:String):void {
				var sheetItem:XML = sheets[sheetName];
				if (sheetItem == null) return;
				var regionItem:XML = <region label={regionName} nodeType="Region"/>;
				regions[sheetName+"_"+regionName] = regionItem;
				sheetItem.appendChild(regionItem);
				timerExpandTree.start();
			}
			
			// we keep a map of sheetName_regionName, and assume that is unique...
			public function addHandlerItem(handlerName:String, regionName:String, sheetName:String):void {
				var regionItem:XML = regions[sheetName+"_"+regionName];
				if (regionItem == null) return;
				regionItem.appendChild(<handler label={handlerName} nodeType="Handler"/>);
				timerExpandTree.start();
			}

			// picks the right icon for the different paper UI components
			public function iconFunction(item:Object):Class {
				// trace("Item: [" + item + "]");
				var xmlNode:XML = item as XML;
				if (xmlNode.@nodeType == "Region") {
					return Images.Region;
				} else if (xmlNode.@nodeType == "Handler") {
					return Images.Handler;
				} else {
					return Images.Sheet;
				}
			}


			// expands the tree in half a second
			public function timerExpandHandler(e:TimerEvent):void {
				timerExpandTree.stop();
				for (var i:int=0; i<paperUITreeData.length; i++) {
					var obj:Object = paperUITreeData.getItemAt(i);
					// trace("expanding item: " + i + " " + obj);
					paperUITree.expandChildrenOf(obj, true);
				}
			}
			
			
			
			
			// this is now zero indexed... so it works!
			public function setPenActive(indx:int):void {
				var penImg:Image = penArray[indx];
				// trace("Active: " + penImg);
				penImg.source = Images.PenActive;
				setCurrentActivePen(indx);
			}
			public function setPenNotActive(indx:int):void {
				var penImg:Image = penArray[indx];
				// trace("Not Active: " + penImg);
				penImg.source = Images.PenInactive;
			}
			private function setCurrentActivePen(indx:int):void {
				currentPen.text = "Pen " + indx;
				sideCar.setStatusMessage("Writing with " + currentPen.text);
			}

			private function treeChanged(event:Event):void {
				selectedNode = (event.target as Tree).selectedItem as XML;
				trace("Tree Changed: " + selectedNode.@label);
			}
			private function treeItemSelected():void {
				trace("Selected Tree Items: " + paperUITree.selectedIndices);
			}
			
			public function goToLastEditLocation():void {
				// isn't the last edit location usually open in eclipse?
				// this doesn't seem to matter, then...
			}
		]]>
	</mx:Script>

		
	<mx:VBox top="10" left="10" width="100%" height="100%" right="10">
		<!-- Image of one or more Pens, with a panel to show pen data as it comes in... //-->
		<mx:HBox top="0" left="0" right="0" width="100%" verticalAlign="top">
			<mx:VBox horizontalAlign="right" verticalAlign="top" verticalGap="0">
				<mx:Label id="currentPen" text="None Active" styleName="detailsText"/>
				<mx:HBox horizontalGap="0" >
					<mx:Image id="pen1" source="{Images.PenInactive}" click="trace('Pen 1 Clicked');" buttonMode="true"/>
					<mx:Image id="pen2" source="{Images.PenInactive}" click="trace('Pen 2 Clicked');" buttonMode="true"/>
					<mx:Image id="pen3" source="{Images.PenInactive}" click="trace('Pen 3 Clicked');" buttonMode="true"/>
					<mx:Image id="pen4" source="{Images.PenInactive}" click="trace('Pen 4 Clicked');" buttonMode="true"/>
				</mx:HBox>
			</mx:VBox>
			<mx:VBox width="100%" height="100%" horizontalAlign="left" verticalGap="0">
				<mx:HBox width="100%">
					<mx:Text styleName="detailsText" id="totalNumStrokes" text="0 strokes"/>
					<mx:Spacer width="70%"/>
					<mx:Text styleName="detailsText" id="currentDuration" text="10 seconds"/>
				</mx:HBox>
				<mx:Canvas id="inkStrokeCanvas"
					backgroundColor="0x000000" width="100%" height="100%" borderThickness="2" 
					borderColor="0x333333" borderStyle="solid" cornerRadius="10"/>
			</mx:VBox>
		</mx:HBox>

		<!-- Tree List of Sheets, Regions, Handlers... to provide quick access //-->
		<mx:Tree id="paperUITree" top="95" left="10" right="10" width="100%" height="100%" 
			labelField="@label" styleName="tree" defaultLeafIcon="{Images.Handler}" 
			iconFunction="iconFunction" change="treeChanged(event);" 
			dataProvider="" click="treeItemSelected()"/>
		<mx:HBox horizontalGap="0" verticalAlign="middle" width="100%">
			<mx:Label styleName="detailsText" text="Last Edit:"/>
			<mx:VBox>
				<mx:LinkButton id="lastClassAndLocation" label="MapSheet : line 114" click="goToLastEditLocation()"/>
				<mx:LinkButton id="lastMethodSelected" label="gestureHandler()"/>
			</mx:VBox>
			<mx:Label styleName="detailsText" text="Last Typed:"/>
			<mx:Text text='System.out.println("Got Here");' selectable="false" width="105" 
				buttonMode="true" useHandCursor="true" click="goToLastEditLocation()"/>
		</mx:HBox>
	</mx:VBox>
</mx:Canvas>
